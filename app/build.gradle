apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'dagger.hilt.android.plugin'

apply from: "$rootProject.projectDir/scripts/detekt.gradle"
apply from: "$rootProject.projectDir/scripts/jacoco.gradle"

android {
    def config = rootProject.extensions.getByName("ext")

    compileSdkVersion config['compile_sdk']
    buildToolsVersion config['build_tools']

    defaultConfig {
        applicationId config["application_id"]
        minSdkVersion config["min_sdk"]
        targetSdkVersion config["target_sdk"]
        versionCode config["version_code"]
        versionName config["version_name"]
        testInstrumentationRunner config["test_runner"]
    }

    testOptions.unitTests.all {
        jvmArgs "-XX:+UnlockExperimentalVMOptions", "-XX:+UseCGroupMemoryLimitForHeap"
    }

    lintOptions {
        quiet true
        abortOnError false
        ignoreWarnings true
        disable 'InvalidPackage'            //Some libraries have issues with this.
        disable 'OldTargetApi'
        //Lint gives this warning but SDK 20 would be Android L Beta.
        disable 'IconDensities'             //For testing purpose. This is safe to remove.
        disable 'IconMissingDensityFolder'  //For testing purpose. This is safe to remove.
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            testCoverageEnabled = false
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            testCoverageEnabled = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        dataBinding = true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        main.java.srcDirs += [file("$buildDir/generated/source/kapt/main")]
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Compile time dependencies
    kapt annotationProcessors.daggerCompiler
    kapt annotationProcessors.hiltCompiler
    kapt annotationProcessors.moshi
    implementation 'androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha01'
    // When using Kotlin.
    kapt 'androidx.hilt:hilt-compiler:1.0.0-alpha01'

    implementation libraries.appCompat
    implementation libraries.core
    implementation libraries.fragment
    implementation libraries.material
    implementation libraries.swipeRefreshLayout
    implementation libraries.constraintLayout
    implementation libraries.lifecycle
    implementation libraries.navigation

    implementation libraries.kotlin
    implementation libraries.hilt
    implementation libraries.timber
    implementation libraries.coroutines
    implementation libraries.liveDataKTX
    implementation libraries.retrofit
    implementation libraries.moshiKotlin
    implementation libraries.okHttpLogger
    implementation libraries.markdown
    implementation libraries.picasso

    testImplementation testLibraries.jUnitApi
    testImplementation testLibraries.mockk
    testImplementation testLibraries.kluent
    testImplementation testLibraries.coroutinesCore
    testImplementation testLibraries.coroutinesTest

    testRuntimeOnly testLibraries.jUnitEngine
}
