apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    def config = rootProject.extensions.getByName("ext")

    compileSdkVersion config['compile_sdk']
    buildToolsVersion config['build_tools']

    defaultConfig {
        applicationId config["application_id"]
        minSdkVersion config["min_sdk"]
        targetSdkVersion config["target_sdk"]
        versionCode config["version_code"]
        versionName config["version_name"]
        testInstrumentationRunner config["test_runner"]
    }

    lintOptions {
        quiet true
        abortOnError false
        ignoreWarnings true
        disable 'InvalidPackage'            //Some libraries have issues with this.
        disable 'OldTargetApi'              //Lint gives this warning but SDK 20 would be Android L Beta.
        disable 'IconDensities'             //For testing purpose. This is safe to remove.
        disable 'IconMissingDensityFolder'  //For testing purpose. This is safe to remove.
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        main.java.srcDirs += [file("$buildDir/generated/source/kapt/main")]
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Compile time dependencies
    kapt annotationProcessors.roomCompiler
    kapt annotationProcessors.daggerCompiler
    kapt annotationProcessors.moshi

    implementation libraries.appCompat
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation libraries.material
    implementation libraries.swipeRefreshLayout
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation libraries.constraintLayout
    implementation libraries.lifecycle

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.61"
    implementation libraries.dagger
    implementation libraries.timber
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.coroutinesVersion}"
    implementation libraries.liveDataKTX
    implementation libraries.retrofit
    implementation libraries.room
    implementation libraries.moshiKotlin
    implementation libraries.okHttpLogger

    testImplementation testLibraries.jUnitApi
    testImplementation testLibraries.mockk
    testImplementation testLibraries.kluent

    testRuntimeOnly testLibraries.jUnitEngine
}
